#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьЭлементыНаФорму(Форма) Экспорт 
	
	ИмяФормы = Форма.ИмяФормы;
	 
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппу(Форма, Форма.Элементы.ГруппаШапкаПраво);
		ДобавитьСогласованнуюСкидкуИКнопкуПересчитатьВГруппу(Форма, Форма.Элементы.ГруппаШапкаЛево);  
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппу(Форма, Форма.Элементы.ГруппаШапкаПраво);
    ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппу(Форма, Форма.Элементы.ГруппаШапкаПраво);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоПередЭлементом(Форма, Форма.Элементы.Основание);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоПередЭлементом(Форма, Форма.Элементы.СуммаДокумента);    
	КонецЕсли;
	
КонецПроцедуры   

Процедура ДобавитьКонтактноеЛицоВГруппу(Форма, ИмяГруппы) Экспорт 
	
	НовоеПоле = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), ИмяГруппы);
    НовоеПоле.Вид = ВидПоляФормы.ПолеВвода;
	НовоеПоле.ПутьКДанным = "Объект.Доп_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДобавитьКонтактноеЛицоПередЭлементом(Форма, ЭлементПередКоторымВставка) Экспорт 
	
	НовоеПоле = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , ЭлементПередКоторымВставка);
    НовоеПоле.Вид = ВидПоляФормы.ПолеВвода;
	НовоеПоле.ПутьКДанным = "Объект.Доп_КонтактноеЛицо";
	
КонецПроцедуры  

Процедура ДобавитьСогласованнуюСкидкуИКнопкуПересчитатьВГруппу(Форма, ИмяГруппы) Экспорт    
	
	ГруппаСкидка = Форма.Элементы.Добавить("Доп_ГруппаСкидка", Тип("ГруппаФормы"), ИмяГруппы);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидка.ОтображатьЗаголовок = ЛОЖЬ; 
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда; 
	ГруппаСкидка.СквозноеВыравнивание = СквозноеВыравнивание.НеИспользовать;
	
	НовоеПоле = Форма.Элементы.Добавить("Доп_СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.Доп_ГруппаСкидка);
    НовоеПоле.Вид = ВидПоляФормы.ПолеВвода;
	НовоеПоле.ПутьКДанным = "Объект.Доп_СогласованнаяСкидка"; 
	НовоеПоле.УстановитьДействие("ПриИзменении", "Доп_СогласованнаяСкидкаПриИзменении");
	
	//Создание команды
	НоваяКоманда = Форма.Команды.Добавить("Доп_ПересчитатьТабличныеЧасти");
	НоваяКоманда.Заголовок = "Пересчитать документ";
	НоваяКоманда.Действие = "Доп_ПересчитатьТабличныеЧастиДокумента";
	
	НоваяКнопка = Форма.Элементы.Добавить("Доп_ПересчитатьТабличныеЧасти", Тип("КнопкаФормы"), Форма.Элементы.Доп_ГруппаСкидка);
	НоваяКнопка.ИмяКоманды = "Доп_ПересчитатьТабличныеЧасти";   
	
КонецПроцедуры


#КонецОбласти